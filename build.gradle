buildscript {
    repositories {
        maven {
            url "https://oss.sonatype.org"
        }
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.ajoberstar', name: 'gradle-plugins', version: '0.1.1'

    }
}


plugins {
  id "eu.appsatori.fatjar" version "0.3"
}

apply plugin: "java"
apply plugin: "eu.appsatori.fatjar"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

group = "com.github.adamyork"

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = "1.4"

fatJar {
    archiveName = "wiremock-velocity-transformer-standalone-" + fatJar.version + ".jar"
    manifest {
        attributes "Implementation-Title"   : "wiremock-velocity-transformer-standalone",
                   "Implementation-Version" : version
    }
}

task cleanFunctional(type: Delete) {
    delete fileTree(dir: "functional", include: "*-velocity-transformer-*.jar", exclude: "wiremock-1.58-standalone.jar")
}

task copyFunctional(type: Copy) {
     from "build/libs/"
     include "*.jar"
     exclude "*-sources.jar","*-javadoc.jar"
     into "functional/"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from "build/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

jar {
    dependsOn fatJar
    dependsOn cleanFunctional
    dependsOn copyFunctional
    cleanFunctional.shouldRunAfter fatJar
    copyFunctional.dependsOn cleanFunctional
    copyFunctional.shouldRunAfter jar
    archiveName = "wiremock-velocity-transformer-" + jar.version + ".jar"
    manifest {
        attributes "Implementation-Title"   : "wiremock-velocity-transformer",
                   "Implementation-Version" : version
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

repositories {
    mavenCentral()
}

signing {
    sign configurations.archives
}

dependencies {
    compile         group: "org.apache.velocity",           name: "velocity",              version: "1.7"
    compile         group: "org.apache.velocity",           name: "velocity-tools",        version: "2.0"
    compile         group: "com.github.tomakehurst",        name: "wiremock",              version: "2.1.12"
    compile         group: "org.eclipse.jetty",             name: "jetty-server",          version: "9.2.13.v20150730"
    compile         group: "org.eclipse.jetty",             name: "jetty-servlet",         version: "9.2.13.v20150730"
    compile         group: "org.eclipse.jetty",             name: "jetty-servlets",        version: "9.2.13.v20150730"
    compile         group: "org.eclipse.jetty",             name: "jetty-webapp",          version: "9.2.13.v20150730"
    compile         group: "com.google.guava",              name: "guava",                 version: "18.0"
    compile         group: "com.fasterxml.jackson.core",    name: "jackson-core",          version: "2.6.1"
    compile         group: "com.fasterxml.jackson.core",    name: "jackson-annotations",   version: "2.6.1"
    compile         group: "com.fasterxml.jackson.core",    name: "jackson-databind",      version: "2.6.1"
    compile         group: "org.apache.httpcomponents",     name: "httpclient",            version: "4.5.1"
    compile         group: "org.xmlunit",                   name: "xmlunit-core",          version: "2.1.1"
    compile         group: "org.xmlunit",                   name: "xmlunit-legacy",        version: "2.1.1"
    compile         group: "com.jayway.jsonpath",           name: "json-path",             version: "2.2.0"
    compile         group: "org.slf4j",                     name: "slf4j-api",             version: "1.7.12"
    compile         group: "net.sf.jopt-simple",            name: "jopt-simple",           version: "4.9"
    compile         group:"org.apache.commons",             name: "commons-lang3",         version: "3.3.4"
    compile         group:"com.flipkart.zjsonpatch",        name: "zjsonpatch",            version: "0.2.1"
    compile ("junit:junit:4.12") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
    }    
    testCompile group: "org.hamcrest",                      name:"hamcrest-all",           version: "1.3"
    testCompile group: "org.skyscreamer",                   name: "jsonassert",            version: "1.2.3"
    testCompile group: "net.sf.json-lib",                   name: "json-lib",              version: "2.4", classifier: 'jdk15'
    testCompile group: "com.googlecode.jarjar",             name: "jarjar",                version: "1.3"
    testCompile group: "commons-io",                        name: "commons-io",            version: "2.4"
    testCompile ("org.jmock:jmock:2.5.1") {
        exclude group: "junit",        module: "junit-dep"
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }
    testCompile ("org.jmock:jmock-junit4:2.5.1") {
        exclude group: "junit",        module: "junit-dep"
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { 
                MavenDeployment deployment -> signing.signPom(deployment)
            }
 
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
 
            pom.project {
               name "wiremock-velocity-transformer"
               packaging "jar"
               description "transformer used to render velocity templates for stubbed responses."
               url "https://github.com/adamyork/wiremock-velocity-transformer"
 
               scm {
                   url "scm:git@github.com:adamyork/wiremock-velocity-transformer.git"
                   connection "scm:git@github.com:adamyork/wiremock-velocity-transformer.git"
                   developerConnection "scm:git@github.com:adamyork/wiremock-velocity-transformer.git"
               }
 
               licenses {
                   license {
                       name "The Apache Software License, Version 2.0"
                       url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                       distribution "repo"
                   }
               }
 
               developers {
                   developer {
                       id "adamcyork"
                       name "Adam York"
                   }
               }
           }
        }
    }
}